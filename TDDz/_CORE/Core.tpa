//=============================================================================
DEFINE_ACTION_MACRO InstallSpecificContents
BEGIN		
	APPEND ~GTIMES.IDS~ ~12 TWO_ROUNDS~ 	UNLESS ~TWO_ROUNDS~
	APPEND ~GTIMES.IDS~ ~18 THREE_ROUNDS~ 	UNLESS ~THREE_ROUNDS~
	APPEND ~GTIMES.IDS~ ~24 FOUR_ROUNDS~ 	UNLESS ~FOUR_ROUNDS~
	APPEND ~GTIMES.IDS~ ~30 FIVE_ROUNDS~ 	UNLESS ~FIVE_ROUNDS~
	APPEND ~GTIMES.IDS~ ~36 SIX_ROUNDS~ 	UNLESS ~SIX_ROUNDS~
	APPEND ~GTIMES.IDS~ ~54 NINE_ROUNDS~ 	UNLESS ~NINE_ROUNDS~
	
	
	COPY_EXISTING ~SPELL.IDS~ ~override~
		REPLACE_TEXTUALLY ~2302 WIZARD_DISPEL_MAGIC~ ~2302 WIZARD_REMOVE_MAGIC~ 
	BUT_ONLY
	APPEND ~SPELL.IDS~ ~2326 WIZARD_DISPEL_MAGIC~ UNLESS ~2326 WIZARD_DISPEL_MAGIC~
 
	
	COPY ~%MOD_FOLDER%/_CORE/SPL/SPSHAD2.spl~ ~override~
	
	ADD_SPELL ~TDD/Spl/SPPR736.spl~ 1 7 CLERIC_SUMMON_LAVA_ELEMENTAL 
		SAY NAME1 @20739
		SAY UNIDENTIFIED_DESC @20740
	ADD_SPELL ~TDD/Spl/SPPR737.SPL~ 1 7
	CLERIC_SUMMON_WATER_ELEMENTAL
		SAY NAME1 @20066 
		SAY UNIDENTIFIED_DESC @20741		
	ADD_SPELL ~TDD/Spl/SPWI430.spl~ 1 6 COM_DAERRAGHS_AURA_OF_CLEANSING
		SAY NAME1 @20847
		SAY UNIDENTIFIED_DESC @20848
	ADD_SPELL ~TDD/Spl/SPPR629.spl~ 1 6 COM_DRUID_SUMMON_LIGHTNING_ELEMENTAL
		SAY NAME1 @20729
		SAY UNIDENTIFIED_DESC @20730		
	ADD_SPELL ~TDD/Spl/SPWI820.spl~ 1 7 COM_HEMOPHILIA
		SAY NAME1 @20875
		SAY UNIDENTIFIED_DESC @20876
	ADD_SPELL ~TDD/Spl/SPWI827.spl~ 2 8 COM_GRAVITY
		SAY NAME1 @20890
		SAY UNIDENTIFIED_DESC @20891
	ADD_SPELL ~TDD/Spl/SPWI427.spl~ 2 4 COM_ICE_SHARD
		SAY NAME1 @20841
		SAY UNIDENTIFIED_DESC @20842
	ADD_SPELL ~TDD/Spl/SPPR525.spl~ 1 5 COM_PRIEST_EMBALM
		SAY NAME1 @20687
		SAY UNIDENTIFIED_DESC @20688
	ADD_SPELL ~TDD/Spl/SPPR623.spl~ 1 6 COM_PRIEST_VISIONS_OF_PAIN
		SAY NAME1 @20716
		SAY UNIDENTIFIED_DESC @20717
	ADD_SPELL ~TDD/Spl/SPPR963.spl~ 1 9 FRIGHTFUL_MOAN
		SAY NAME1 @20814
	ADD_SPELL ~TDD/Spl/SPPR962.spl~ 1 7 TOPAZ_DRAGON_BREATH 
		SAY NAME1 @20813				
	ADD_SPELL ~TDD/Compat/CtB/SPWI627.spl~ 2 6
	WIZARD_ANTIMAGIC_FIELD
		SAY NAME1 @20946
		SAY UNIDENTIFIED_DESC @20947
	
	//ACTION_IF NOT FILE_EXISTS ~data/CTB-RULE.BIF~ THEN BEGIN //CtB compatibility
	// Hence we rename the spells, we HAVE TO do import them with or without CtB
		COPY ~%MOD_FOLDER%/_CORE/SPL/SPWI823.spl~ ~override/SPTDD01.spl~ // ADD_SPELL failed for WIZARD_IRON_BODY...
			SAY NAME1 @20948
			SAY UNIDENTIFIED_DESC @20949
		COPY ~%MOD_FOLDER%/_CORE/SPL/SPWI824.spl~ ~override/SPTDD02.spl~ // ADD_SPELL failed for WIZARD_MIND_BLANK...
			SAY NAME1 @20950
			SAY UNIDENTIFIED_DESC @20951
	//END
		
		
	LAM ImportSpellScrolls_FromTDD
	
	ACTION_IF GAME_IS ~tob~ BEGIN		
		ACTION_IF NOT FILE_EXISTS ~data/ROT-RULE.BIF~ BEGIN    // Compatibility with RoT	
			COPY ~TDD/Compat/anim/MCYC~ ~override~          //CYCLOP
			COPY ~TDD/Compat/anim/MTAN~ ~override~          //MARILITH graphics
			COPY ~TDD/Compat/anim/MNO2~ ~override~          //FROST GIANT

			COPY_EXISTING 	~ORC06.cre~     ~override~
							~HLOLAF.cre~    ~override~
							~CHAK.cre~      ~override~
							~AR18FIG.cre~   ~override~
				WRITE_LONG 0x28  0xE700   //IC_OROG1 instead of IC_OROG2
			BUT_ONLY_IF_IT_CHANGES
  
			ACTION_IF (FILE_EXISTS_IN_GAME ~ULIGAR.CRE~) BEGIN
				COPY_EXISTING ~ULIGAR.CRE~        ~override~    //NeJ2
					WRITE_LONG 0x28  0xE700   //IC_OROG1 instead of IC_OROG2
				BUT_ONLY_IF_IT_CHANGES
			END
		END
	END	
	
	ACTION_IF GAME_IS ~bg2ee eet~ BEGIN
		LAM InstallFrostGiantAnimation
		LAM InstallIronGolemAnimation
		LAM InstallCyclopsAnimation	
	END
			
	INCLUDE ~%MOD_FOLDER%/Lib/Items.tpa~ 
		LAM UpdateTooltip
		
	INCLUDE ~%MOD_FOLDER%/Worldmap/Worldmap.tpa~
		LAM NotifyWorldmap	
		
	OUTER_TEXT_SPRINT sender ~%COMP_FOLDER%~
	INCLUDE ~%MOD_FOLDER%/Lib/Helpers.tpa~ LAF Accept STR_VAR sender target = ~_CORE~ END	
END

//=============================================================================
DEFINE_ACTION_MACRO InstallIronGolemAnimation
BEGIN
	APPEND ~EXTANIM.2DA~ ~57936 0 1 1 0 0 0 0 0 0 NONE NONE NONE 8 4 MGIR NONE 0 0 NONE~ UNLESS ~57936~	
	APPEND ~ANIMATE.IDS~ ~0xE250 IC_GOLEM_IRON~ UNLESS ~0xE250~	
	
	COPY ~%MOD_FOLDER%/%COMP_FOLDER%/Creatures/IronGolem/BAM~ ~override~
END

//=============================================================================
DEFINE_ACTION_MACRO InstallFrostGiantAnimation
BEGIN
	APPEND ~EXTANIM.2DA~ ~57935 0 1 1 0 0 0 47 0 0 NONE NONE NONE 8 5 MGFR NONE 0 0 NONE~ UNLESS ~57935~
	APPEND ~ANIMATE.IDS~ ~0xE24F IC_GIANT_FROST~ UNLESS ~0xE24F~
	APPEND ~EXTSPEED.2DA~ ~57935	8~ UNLESS ~57935~
		
	COPY ~%MOD_FOLDER%/%COMP_FOLDER%/Creatures/FrostGiant/BAM~ ~override~
	COPY ~%MOD_FOLDER%/%COMP_FOLDER%/Creatures/FrostGiant/MGFR.2DA~ ~override~
	COPY ~%MOD_FOLDER%/%COMP_FOLDER%/Creatures/FrostGiant/OGG~ ~TDDz/Audio~	
END

//=============================================================================
DEFINE_ACTION_MACRO InstallCyclopsAnimation
BEGIN
	APPEND ~ANIMATE.IDS~ ~0xE000 IC_CYCLOPS~ UNLESS ~0xE000~
			
	COPY ~%MOD_FOLDER%/%COMP_FOLDER%/Creatures/Cyclops/BAM~ ~override~	
	COPY ~%MOD_FOLDER%/%COMP_FOLDER%/Creatures/Cyclops/MCYC.2DA~ ~override~
	COPY ~%MOD_FOLDER%/%COMP_FOLDER%/Creatures/Cyclops/OGG~ ~TDDz/Audio~
END




//=============================================================================
DEFINE_ACTION_MACRO ImportSpellScrolls_FromTDD
BEGIN
	COPY ~%MOD_FOLDER%/_CORE/SpellScrollsContract.2da~ ~%MOD_FOLDER%/TEMP/%COMP_FOLDER%/SpellScrollsContract.2da~
		COUNT_2DA_ROWS 8 count
		WHILE (count > 1) BEGIN
			READ_2DA_ENTRY (count -1) 0 7 resname
			READ_2DA_ENTRY (count -1) 1 7 name1
			READ_2DA_ENTRY (count -1) 2 7 name2
			READ_2DA_ENTRY (count -1) 3 7 unidentifiedDesc
			READ_2DA_ENTRY (count -1) 4 7 desc
			READ_2DA_ENTRY (count -1) 5 7 iconS
			READ_2DA_ENTRY (count -1) 6 7 policy //undroppable column
			READ_2DA_ENTRY (count -1) 7 7 path_to_file
			SET count = count -1
			
			TO_UPPER resname
			TO_UPPER policy
			SET unchanged = (~%policy%~ STRING_EQUAL ~+~ || ~%policy%~ STRING_EQUAL ~++++++++~)			
			SET undroppable = (~%policy%~ STRING_EQUAL ~.~)
			SET replace = (~%customize%~ && NOT ~%undroppable%~)
			SPRINT newItem ~%policy%~
			PATCH_IF (~%undroppable%~)
				BEGIN SPRINT newItem ~SCRL75~ END
										
			INNER_ACTION BEGIN					
				ACTION_IF ~%unchanged%~ BEGIN 
					COPY ~TDD/%path_to_file%/%resname%.itm~ ~override~
						PATCH_IF IS_AN_INT name1 BEGIN SET strref = RESOLVE_STR_REF ((AT name1)) WRITE_LONG NAME1 %strref% END
						PATCH_IF IS_AN_INT name2 BEGIN SET strref = RESOLVE_STR_REF ((AT name2)) WRITE_LONG NAME2 %strref% END
						PATCH_IF IS_AN_INT unidentifiedDesc BEGIN SET strref = RESOLVE_STR_REF ((AT unidentifiedDesc)) WRITE_LONG UNIDENTIFIED_DESC %strref% END
						PATCH_IF IS_AN_INT desc BEGIN SET strref = RESOLVE_STR_REF ((AT desc)) WRITE_LONG DESC %strref% END
						PATCH_IF NOT ~%iconS%~ STRING_EQUAL ~+~ BEGIN 
							PATCH_IF IS_AN_INT iconS 
								BEGIN SPRINT iconS ~%resname%~ END
							WRITE_ASCIIE 0x3A ~%iconS%~ #8
							
							INNER_ACTION BEGIN
								ACTION_IF NOT FILE_EXISTS_IN_GAME ~%iconS%.bam~ BEGIN
									ACTION_IF FILE_EXISTS ~TDD/BAM/%iconS%.bam~ BEGIN 
										COPY ~TDD/BAM/%iconS%.bam~ ~override~ 
									END ELSE BEGIN 
										PRINT ~Warning: missing ressource from TDD and BG2EE: %iconS%.bam, to continue, please press Enter~
										ACTION_READLN ~pause~
									END
								END
							END									
						END				
				END
			END
		END
END
